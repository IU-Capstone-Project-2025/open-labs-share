plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.5'
}


group = 'olsh.backend'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springGrpcVersion', "0.8.0")
	set('grpcVersion', "1.72.0")  // Add this for consistency
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-web' // REST API
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation' // Requests Validation
	implementation 'org.springframework.boot:spring-boot-starter-aop' // Custom Annotations
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9' // @ParameterObject && Swagger
	implementation 'org.springframework.boot:spring-boot-starter-actuator' // Actuator

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.34'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.grpc:grpc-testing:1.72.0'
	testImplementation 'com.google.protobuf:protobuf-java-util:4.28.2'

	// gRPC client
	implementation 'org.springframework.grpc:spring-grpc-client-spring-boot-starter'
	implementation 'io.grpc:grpc-services'
	implementation 'com.google.protobuf:protobuf-java-util:4.28.2'

	// Jakarta annotations for Java 21
	compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
	}
}

// Protobuf task configuration
protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:4.28.2"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.72.0"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc {
					option 'jakarta_omit'
					option '@generated=omit'
				}
			}
		}
	}
}

// Add generated sources to source sets
sourceSets {
	main {
		java {
			srcDirs 'build/generated/sources/proto/main/java'
			srcDirs 'build/generated/sources/proto/main/grpc'
		}
	}
}



tasks.named('test') {
	useJUnitPlatform()
}

test {
	afterSuite { desc, result ->
		if (!desc.parent) {
			println("Results: ${result.resultType} " +
					"(${result.testCount} tests, " +
					"${result.successfulTestCount} successes, " +
					"${result.failedTestCount} failures, " +
					"${result.skippedTestCount} skipped)")
		}
	}
}
